namespace = flavor_por
normal_or_historical_nations = yes
country_event = {
  id = flavor_por.3266
  title = "flavor_por.EVTNAME3266"
  desc = "flavor_por.EVTDESC3266"
  picture = COLONIZATION_eventPicture
  fire_only_once = yes
  trigger = {
    tag = POR
    colony = 1
  }
  mean_time_to_happen = {
    months = 120
  }
  option = {
    name = "flavor_por.EVTOPTA3266"
    ai_chance = {
      factor = 75
    }
    add_corruption = 1
    add_country_modifier = {
      name = colonial_policy
      duration = -1
    }
  }
  option = {
    name = "flavor_por.EVTOPTB3266"
    ai_chance = {
      factor = 25
    }
    if = {
      limit = {
        corruption = 0.01
      }
      add_corruption = -1
    }
    if = {
      limit = {
        NOT = {
          corruption = 1
        }
      }
      add_country_modifier = {
        name = less_colonial_corruption
        duration = -1
      }
    }
  }
}
country_event = {
  id = flavor_por.3267
  title = "flavor_por.EVTNAME3267"
  desc = "flavor_por.EVTDESC3267"
  picture = COURT_eventPicture
  fire_only_once = yes
  trigger = {
    tag = POR
    has_regency = no
    NOT = {
      is_year = 1460
    }
  }
  mean_time_to_happen = {
    months = 12
  }
  option = {
    name = "flavor_por.EVTOPTA3267"
    ai_chance = {
      factor = 75
    }
    add_stability_or_adm_power = yes
  }
  option = {
    name = "flavor_por.EVTOPTB3267"
    ai_chance = {
      factor = 25
    }
    add_ruler_modifier = {
      name = duke_of_coimbra
      duration = -1
    }
  }
}
country_event = {
  id = flavor_por.3268
  title = "flavor_por.EVTNAME3268"
  desc = "flavor_por.EVTDESC3268"
  picture = MERCHANTS_TALKING_eventPicture
  fire_only_once = yes
  trigger = {
    tag = POR
    any_active_trade_node = {
      province_id = 4346
      has_merchant = prev
    }
  }
  mean_time_to_happen = {
    months = 12
  }
  option = {
    name = "flavor_por.EVTOPTA3268"
    ai_chance = {
      factor = 10
    }
    random_active_trade_node = {
      limit = {
        province_id = 4346
      }
      add_trade_modifier = {
        who = root
        duration = -1
        power = 25
        key = portuguese_privateers
      }
    }
  }
  option = {
    name = "flavor_por.EVTOPTB3268"
    ai_chance = {
      factor = 0
    }
    custom_tooltip = flavor_por.3268.b.tt
    hidden_effect = {
      random_active_trade_node = {
        limit = {
          province_id = 4346
        }
        every_trade_node_member_country = {
          add_opinion = {
            who = ROOT
            modifier = careful_trade_expansion
          }
        }
      }
    }
  }
}
country_event = {
  id = flavor_por.3269
  title = "flavor_por.EVTNAME3269"
  desc = "flavor_por.EVTDESC3269"
  picture = POPE_PREACHING_eventPicture
  fire_only_once = yes
  trigger = {
    tag = POR
    religion = catholic
    exists = PAP
    is_year = 1500
    NOT = {
      is_year = 1600
    }
  }
  mean_time_to_happen = {
    months = 120
  }
  option = {
    name = "flavor_por.EVTOPTA3269"
    ai_chance = {
      factor = 75
    }
    add_papal_influence = 20
    add_country_modifier = {
      name = narrowminded_modifier
      duration = 3650
    }
  }
  option = {
    name = "flavor_por.EVTOPTB3269"
    ai_chance = {
      factor = 25
    }
    PAP = {
      add_opinion = {
        who = ROOT
        modifier = no_holy_inquisition
      }
    }
  }
}
country_event = {
  id = flavor_por.3271
  title = "flavor_por.EVTNAME3271"
  desc = "flavor_por.EVTDESC3271"
  picture = COLONIZATION_eventPicture
  fire_only_once = yes
  trigger = {
    tag = POR
    is_year = 1530
    NOT = {
      is_year = 1580
    }
    brazil_region = {
      country_or_vassal_holds = POR
    }
  }
  mean_time_to_happen = {
    months = 100
  }
  option = {
    name = "flavor_por.EVTOPTA3271"
    add_country_modifier = {
      name = governor_de_sousa
      duration = 3650
    }
  }
}
country_event = {
  id = flavor_por.3272
  title = "flavor_por.EVTNAME3272"
  desc = "flavor_por.EVTDESC3272"
  picture = SHIP_SAILING_eventPicture
  fire_only_once = yes
  trigger = {
    tag = POR
    is_year = 1500
    religion = catholic
    is_free_or_tributary_trigger = yes
    MOR = {
      exists = yes
      num_of_ports = 1
      religion_group = muslim
      is_free_or_tributary_trigger = yes
      NOT = {
        war_with = POR
      }
      NOT = {
        truce_with = POR
      }
      NOT = {
        alliance_with = POR
      }
    }
  }
  mean_time_to_happen = {
    months = 100
  }
  option = {
    name = "flavor_por.EVTOPTA3272"
    ai_chance = {
      factor = 90
    }
    add_papal_influence = 10
    MOR = {
      every_owned_province = {
        limit = {
          is_in_capital_area = yes
          has_port = yes
        }
        add_claim = POR
      }
    }
    MOR = {
      add_opinion = {
        who = ROOT
        modifier = prepares_crusade
      }
    }
  }
  option = {
    name = "flavor_por.EVTOPTB3272"
    ai_chance = {
      factor = 0
    }
    add_prestige = -1
  }
}
country_event = {
  id = flavor_por.3273
  title = "flavor_por.EVTNAME3273"
  desc = "flavor_por.EVTDESC3273"
  picture = PLAGUE_eventPicture
  fire_only_once = yes
  trigger = {
    tag = POR
    num_of_cities = 4
    iberia_region = {
      owned_by = ROOT
    }
    is_year = 1550
    NOT = {
      is_year = 1700
    }
  }
  mean_time_to_happen = {
    months = 120
  }
  option = {
    name = "flavor_por.EVTOPTA3273"
    add_country_modifier = {
      name = severe_plague
      duration = 1825
    }
  }
}
country_event = {
  id = flavor_por.3275
  title = "flavor_por.EVTNAME3275"
  desc = "flavor_por.EVTDESC3275"
  picture = DIPLOMACY_eventPicture
  fire_only_once = yes
  trigger = {
    tag = POR
    is_year = 1650
    is_at_war = no
    OR = {
      exists = GBR
      exists = ENG
    }
    iberia_region = {
      owned_by = ROOT
      has_port = yes
    }
    NOT = {
      war_with = ENG
    }
    NOT = {
      is_rival = ENG
    }
    NOT = {
      war_with = GBR
    }
    NOT = {
      is_rival = GBR
    }
  }
  mean_time_to_happen = {
    months = 100
  }
  option = {
    name = "flavor_por.EVTOPTA3275"
    ai_chance = {
      factor = 75
    }
    if = {
      limit = {
        exists = GBR
      }
      GBR = {
        add_opinion = {
          who = ROOT
          modifier = accepted_meuthen_agreement
        }
      }
      set_country_flag = meuthen_agreement
    }
    if = {
      limit = {
        exists = ENG
        NOT = {
          has_country_flag = meuthen_agreement
        }
      }
      ENG = {
        add_opinion = {
          who = ROOT
          modifier = accepted_meuthen_agreement
        }
      }
    }
    random_owned_province = {
      limit = {
        region = iberia_region
        has_port = yes
      }
      add_base_tax = 1
    }
    clr_country_flag = meuthen_agreement
  }
  option = {
    name = "flavor_por.EVTOPTB3275"
    ai_chance = {
      factor = 25
    }
    add_stability_or_adm_power = yes
    if = {
      limit = {
        exists = GBR
      }
      GBR = {
        add_opinion = {
          who = ROOT
          modifier = denounced_meuthen_agreement
        }
      }
      set_country_flag = meuthen_agreement
    }
    if = {
      limit = {
        exists = ENG
        NOT = {
          has_country_flag = meuthen_agreement
        }
      }
      ENG = {
        add_opinion = {
          who = ROOT
          modifier = denounced_meuthen_agreement
        }
      }
    }
    clr_country_flag = meuthen_agreement
  }
}
country_event = {
  id = flavor_por.3276
  title = "flavor_por.EVTNAME3276"
  desc = "flavor_por.EVTDESC3276"
  picture = ADVISOR_eventPicture
  fire_only_once = yes
  trigger = {
    tag = POR
    is_year = 1662
    NOT = {
      is_year = 1690
    }
  }
  mean_time_to_happen = {
    months = 100
  }
  option = {
    name = "EXCELLENTMINISTER"
    add_dip_power = 100
    define_advisor = {
      type = statesman
      name = "U ÔçfØe ∑V ÖhÖQ^XØe"  # Dom LuÌs de Meneses
      culture = portugese
      discount = yes
      skill = 2
    }
  }
}
country_event = {
  id = flavor_por.3278
  title = "flavor_por.EVTNAME3278"
  desc = "flavor_por.EVTDESC3278"
  picture = REVOLUTION_eventPicture
  fire_only_once = yes
  trigger = {
    tag = POR
    owns = 227
    is_year = 1700
  }
  mean_time_to_happen = {
    months = 2000
  }
  option = {
    name = "flavor_por.EVTOPTA3278"
    227 = {
      add_devastation = 100
      add_province_modifier = {
        name = earthquake
        duration = 3650
      }
    }
  }
}
country_event = {
  id = flavor_por.3279
  title = "flavor_por.EVTNAME3279"
  desc = "flavor_por.EVTDESC3279"
  picture = ADVISOR_eventPicture
  fire_only_once = yes
  trigger = {
    tag = POR
    is_year = 1729
    NOT = {
      is_year = 1777
    }
  }
  mean_time_to_happen = {
    months = 30
  }
  immediate = {
    set_country_flag = POR_de_pombal
  }
  option = {
    name = "EXCELLENTMINISTER"
    add_adm_power = 100
    add_dip_power = 100
    add_mil_power = 100
  }
}
country_event = {
  id = flavor_por.3280
  title = "flavor_por.EVTNAME3280"
  desc = "flavor_por.EVTDESC3280"
  picture = ASSASSINATION_eventPicture
  fire_only_once = yes
  trigger = {
    tag = POR
    iberia_region = {
      owned_by = POR
    }
    had_country_flag = {
      flag = POR_de_pombal
      days = 1000
    }
  }
  mean_time_to_happen = {
    months = 64
  }
  option = {
    name = "flavor_por.EVTOPTA3280"
    ai_chance = {
      factor = 75
    }
    random_owned_province = {
      limit = {
        region = iberia_region
        is_capital = no
      }
      spawn_rebels = {
        type = noble_rebels
        size = 2
      }
    }
  }
  option = {
    name = "flavor_por.EVTOPTB3280"
    ai_chance = {
      factor = 25
    }
    add_legitimacy = -25
    add_republican_tradition = -25
  }
}
country_event = {
  id = flavor_por.3281
  title = "flavor_por.EVTNAME3281"
  desc = "flavor_por.EVTDESC3281"
  picture = CIVIL_WAR_eventPicture
  fire_only_once = yes
  trigger = {
    tag = POR
    iberia_region = {
      owned_by = POR
    }
    had_country_flag = {
      flag = POR_de_pombal
      days = 3000
    }
  }
  mean_time_to_happen = {
    months = 32
  }
  immediate = {
    clr_country_flag = POR_de_pombal
  }
  option = {
    name = "flavor_por.EVTOPTA3281"
    ai_chance = {
      factor = 75
    }
    add_ruler_modifier = {
      name = reactionary_policies
      duration = -1
    }
  }
  option = {
    name = "flavor_por.EVTOPTB3281"
    ai_chance = {
      factor = 25
    }
    every_owned_province = {
      limit = {
        region = iberia_region
        is_capital = no
      }
      add_local_autonomy = 10
    }
  }
}
country_event = {
  id = flavor_por.1
  title = "flavor_por.EVTNAME1"
  desc = "flavor_por.EVTDESC1"
  picture = SHIP_SAILING_eventPicture
  fire_only_once = yes
  trigger = {
    tag = POR
    exploration_ideas = 2
    NOT = {
      has_global_flag = columbus_happened
    }
    is_year = 1470
    OR = {
      NOT = {
        north_america = {
          has_discovered = ROOT
        }
      }
      NOT = {
        south_america = {
          has_discovered = ROOT
        }
      }
    }
  }
  mean_time_to_happen = {
    months = 1
  }
  option = {
    name = "flavor_por.EVTOPTA1"
    ai_chance = {
      factor = 75
    }
    set_country_flag = turned_down_columbus
    if = {
      limit = {
        exists = SPA
        SPA = {
          has_idea = quest_for_the_new_world
        }
      }
      SPA = {
        country_event = {
          id = flavor_spa.1
          days = 10
        }
      }
    }
    if = {
      limit = {
        NOT = {
          exists = SPA
        }
        exists = CAS
        CAS = {
          has_idea = quest_for_the_new_world
        }
      }
      CAS = {
        country_event = {
          id = flavor_spa.1
          days = 10
        }
      }
    }
    if = {
      limit = {
        OR = {
          AND = {
            NOT = {
              exists = SPA
            }
            NOT = {
              exists = CAS
            }
          }
          AND = {
            CAS = {
              NOT = {
                has_idea = quest_for_the_new_world
              }
            }
            SPA = {
              NOT = {
                has_idea = quest_for_the_new_world
              }
            }
          }
        }
        any_known_country = {
          has_idea = quest_for_the_new_world
          religion = catholic
        }
      }
      random_known_country = {
        limit = {
          NOT = {
            has_country_flag = turned_down_columbus
          }
          religion = catholic
          has_idea = quest_for_the_new_world
        }
        country_event = {
          id = flavor_spa.1
        }
      }
    }
  }
  option = {
    name = "flavor_por.EVTOPTB1"
    ai_chance = {
      factor = 25
    }
    set_global_flag = columbus_happened
    add_years_of_income = -0.10
    define_explorer = {
      name = "KQÃëØeXb˙e ÂT&O^"  # CristÛv„o Colombo
      fire = 2
      shock = 2
      manuever = 6
      siege = 0
    }
  }
}
country_event = {
  id = flavor_por.2
  title = "flavor_por.EVTNAME2"
  desc = "flavor_por.EVTDESC2"
  picture = NATIVES_eventPicture
  fire_only_once = yes
  trigger = {
    tag = POR
    colony = 1
    south_america = {
      country_or_vassal_holds = ROOT
    }
    OR = {
      guinea_region = {
        owned_by = ROOT
      }
      south_africa_region = {
        owned_by = ROOT
      }
    }
  }
  mean_time_to_happen = {
    months = 100
  }
  option = {
    name = "flavor_por.EVTOPTA2"
    ai_chance = {
      factor = 90
    }
    add_country_modifier = {
      name = african_slaves
      duration = 18250
    }
  }
  option = {
    name = "flavor_por.EVTOPTB2"
    ai_chance = {
      factor = 10
    }
    add_prestige = 10
  }
}
country_event = {
  id = flavor_por.3
  title = "flavor_por.EVTNAME3"
  desc = "flavor_por.EVTDESC3"
  picture = RELIGION_eventPicture
  fire_only_once = yes
  trigger = {
    tag = POR
    is_year = 1500
    has_discovered = 646
    religion = catholic
    num_of_owned_provinces_with = {
      region = moluccas_region
      NOT = {
        religion = catholic
      }
      value = 2
    }
  }
  mean_time_to_happen = {
    months = 300
  }
  option = {
    name = "flavor_por.EVTOPTA3"
    random_owned_province = {
      limit = {
        region = moluccas_region
        NOT = {
          religion = catholic
        }
      }
      change_religion = catholic
    }
    random_owned_province = {
      limit = {
        region = moluccas_region
        NOT = {
          religion = catholic
        }
      }
      change_religion = catholic
    }
  }
}
country_event = {
  id = flavor_por.4
  title = "flavor_por.EVTNAME4"
  desc = "flavor_por.EVTDESC4"
  picture = MERCHANTS_TALKING_eventPicture
  fire_only_once = yes
  trigger = {
    OR = {
      AND = {
        NOT = {
          has_dlc = "Golden Century"
        }
        tag = POR
        has_country_modifier = india_trade_co
      }
      has_country_flag = por_india_mission_flag
    }
  }
  mean_time_to_happen = {
    months = 12
  }
  option = {
    name = "flavor_por.EVTOPTA4"
    add_mercantilism = 10
    add_country_modifier = {
      name = "portuguese_india_trade_co"
      duration = -1
    }
  }
}
country_event = {
  id = flavor_por.5
  title = "flavor_por.EVTNAME5"
  desc = "flavor_por.EVTDESC5"
  picture = SHIP_SAILING_eventPicture
  fire_only_once = yes
  trigger = {
    tag = POR
    num_of_ports = 1
    exists = NED
    NOT = {
      is_year = 1650
    }
    any_active_trade_node = {
      any_trade_node_member_province = {
        OR = {
          region = bengal_region
          region = deccan_region
          region = coromandel_region
          region = west_india_region
          region = malaya_region
          region = moluccas_region
          region = indonesia_region
          region = oceanea_region
        }
      }
    }
    NED = {
      any_active_trade_node = {
        any_trade_node_member_province = {
          OR = {
            region = bengal_region
            region = deccan_region
            region = coromandel_region
            region = west_india_region
            region = malaya_region
            region = moluccas_region
            region = indonesia_region
            region = oceanea_region
          }
        }
      }
    }
    NED = {
      is_free_or_tributary_trigger = yes
    }
    NOT = {
      alliance_with = NED
    }
    NOT = {
      war_with = NED
    }
  }
  mean_time_to_happen = {
    months = 100
  }
  option = {
    name = "flavor_por.EVTOPTA5"
    ai_chance = {
      factor = 90
    }
    add_prestige = 10
    hidden_effect = {
      NED = {
        country_event = {
          id = flavor_por.6
          days = 10
        }
      }
    }
  }
  option = {
    name = "flavor_por.EVTOPTB5"
    ai_chance = {
      factor = 10
    }
    add_prestige = -10
  }
}
country_event = {
  id = flavor_por.6
  title = "flavor_por.EVTNAME6"
  desc = "flavor_por.EVTDESC6"
  picture = SHIP_SAILING_eventPicture
  is_triggered_only = yes
  option = {
    name = "flavor_por.EVTOPTA6"
    ai_chance = {
      factor = 90
    }
    add_years_of_income = 0.5
    hidden_effect = {
      POR = {
        country_event = {
          id = flavor_por.8
          days = 10
        }
      }
    }
  }
  option = {
    name = "flavor_por.EVTOPTB6"
    ai_chance = {
      factor = 10
    }
    add_prestige = -10
    hidden_effect = {
      POR = {
        country_event = {
          id = flavor_por.7
          days = 10
        }
      }
    }
  }
}
country_event = {
  id = flavor_por.7
  title = "flavor_por.EVTNAME7"
  desc = "flavor_por.EVTDESC7"
  picture = SHIP_SAILING_eventPicture
  is_triggered_only = yes
  option = {
    name = "flavor_por.EVTOPTA7"
    add_years_of_income = 0.5
  }
}
country_event = {
  id = flavor_por.8
  title = "flavor_por.EVTNAME8"
  desc = "flavor_por.EVTDESC8"
  picture = SHIP_SAILING_eventPicture
  is_triggered_only = yes
  option = {
    name = "flavor_por.EVTOPTA8"
    add_casus_belli = {
      target = NED
      type = cb_insult
      months = 12
    }
  }
}
country_event = {
  id = flavor_por.9
  title = "flavor_por.EVTNAME9"
  desc = "flavor_por.EVTDESC9"
  picture = FORT_eventPicture
  fire_only_once = yes
  trigger = {
    tag = POR
    owns = 227
    is_year = 1490
    NOT = {
      is_year = 1520
    }
  }
  mean_time_to_happen = {
    months = 100
  }
  option = {
    name = "flavor_por.EVTOPTA9"
    ai_chance = {
      factor = 90
      modifier = {
        factor = 0
        AND = {
          NOT = {
            treasury = 500
          }
          has_dlc = "Leviathan"
        }
      }
    }
    if = {
      limit = {
        has_dlc = "Leviathan"
      }
      add_treasury = -500
      227 = {
        set_great_project_tier_1 = {
          type = belem_tower
        }
        add_permanent_province_modifier = {
          name = the_tower_or_belem
          duration = -1
        }
      }
    }
    else = {
      add_treasury = -50
      227 = {
        add_permanent_province_modifier = {
          name = the_tower_or_belem
          duration = -1
        }
      }
    }
  }
  option = {
    name = "flavor_por.EVTOPTB9"
    ai_chance = {
      factor = 10
    }
    add_prestige = -1
  }
}
country_event = {
  id = flavor_por.10
  title = "flavor_por.EVTNAME10"
  desc = "flavor_por.EVTDESC10"
  picture = ST_PETERS_CHURCH_eventPicture
  fire_only_once = yes
  trigger = {
    tag = POR
    is_year = 1490
    NOT = {
      is_year = 1520
    }
  }
  mean_time_to_happen = {
    months = 60
  }
  option = {
    name = "flavor_por.EVTOPTA10"
    ai_chance = {
      factor = 90
    }
    add_years_of_income = -0.25
    add_papal_influence = 20
  }
  option = {
    name = "flavor_por.EVTOPTB10"
    ai_chance = {
      factor = 10
    }
    add_prestige = -1
  }
}
country_event = {
  id = flavor_por.11
  title = "flavor_por.EVTNAME11"
  desc = "flavor_por.EVTDESC11"
  picture = SHIP_SAILING_eventPicture
  is_triggered_only = yes
  option = {
    name = "flavor_por.EVTOPTA11"
    trigger = {
      2030 = {
        owner = {
          ai = yes
        }
      }
    }
    ai_chance = {
      factor = 90
      modifier = {
        factor = 0
        overextension_percentage = 1.5
      }
    }
    2030 = {
      cede_province = POR
    }
    add_claim = 2030
    add_prestige = 10
  }
  option = {
    name = "flavor_por.EVTOPTB11"
    trigger = {
      2030 = {
        owner = {
          ai = no
        }
      }
    }
    ai_chance = {
      factor = 10
    }
    add_claim = 2030
    add_prestige = 10
  }
}